//===== eAthena Script ======================================= 
//= Kafra Functions
//===== By: ================================================== 
//= eAthena Dev Team
//= Darlskies, Darkchild, Syrus22, Lupus, kobra_k88 (2.0)
//===== Current Version: ===================================== 
//= 5.4b
//===== Compatible With: ===================================== 
//= eAthena 1.0
//===== Description: =========================================
//= These functions handle save, storage, cart rental, teleport,
//= and Free Teleport/Warp/Rent Cart options for all Kafra NPCs.
//===== Additional Comments: ================================= 
//= v1.1 Now using functions v2.1 Added Cart Rent for Classes: Whitesmith, Professor.
//= Replaced checkoption(x) into checkcart(0) [Lupus] v2.1b Added Fix Kafra Pass Func [Kobra_k88]
//= 2.2 Final fix of the Kafra Pass Exploit! [Lupus] a -Izlude[4] fix
//= 2.2a Minor changes to function calls.  Using agruments. Added Guild options. [kobra_k88]
//= 2.2b This version uses arrays for the teleport option. Rearranged next statements to make menu transitions smoother. [kobra_k88]
//= 2.3 Removed SAVE from Niflheim. [Lupus]
//= 2.3 removed "fix" by HawkMoon RTFM and check supernovice.txt . There's a SPECIAL Kafra which gives CARTS to SN for a special proce. [Lupus]
//= 2.4 Added Baby Class Support (Baby Novice check) Removed annoying storage feature where u had to close dialog window to be able to use your storage [Lupus]
//= 2.5 Added Louyang official Kafra, fixes some Kafras, not letting you to Save your position [Lupus]
//= 2.6 Reverted Dungeons Kafras (they should offer only Storage). Added temp Ayothaya Kafra
//= 2.7 Added correct Ayothaya, Louyang & Amatsu Kafras. [Lupus]
//= 2.8 Fixed Amatsu Storage problems [Lupus] 2.9 Fixed spelling mistakes. [Nexon]
//= 3.0 Added special "not working teleport menu" for Einbroch Kafras [Lupus]
//= 3.1 Removed callfunc Is_####_Class in favor of baseClass [Silentdragon]
//= 3.2 Fixed an exploit [Lupus]
//= 4.0 Added Kafra storage password protection. [Lupus]
//=	to block Kafra Password, read comments at 350 line
//=	Note: You can change '1337' value to another to raise password protection
//= 5.0 Fixed the close2;....close; end; lines. Who let them slip away? =/ [erKURITA]
//=	  Also, the kafra upon warp was giving back the zeny. Removed. [erKURITA]
//= 5.1 Optimized a little, added no tele/save arg's [Evera]
//= 5.1a Temporary? Added F_ClearGarbage to clear unused/outdated variables [Lupus]
//= 5.2 By default commented out custom Guilds Kafra's "Guild Storage". [Lupus]
//= 5.3 uncommented Guild Storage. Confirmed kRO feature. [Lupus]
//= 5.4 added -Guide option (Kafra shows you nearest Guide) Work in progress. 
//=	Need all coords for all guides.. and somehow pass their coords to kafra.
//=	Removed universal Kafra Pass, added 3 new Kafra Tickets [Lupus]
//= 5.4b temporary moved "-Guide" from the 1st menu punct [Lupus]
//= Traduit pour Agora-Online par Arkana
//============================================================ 


// Main Function ===========================================================
//=   arg(0): Used to determine which welcome message to show.
//=   arg(1): Used to determine which menu to display.
//=   arg(2): Used to determine if the info menu is shown in F_KafInfo.
//==========================================================================
function	script	F_Kafra	{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables

	mes "[Kafra]";
	switch(getarg(0)){
		default:
		case 0:
			mes "Bienvenue chez Kafra Corp. Nous serons à vos côtés, où que vous soyez.";
			break;
		//Niflheim
		case 1:
			mes "Bienvenue...Les services Kafra....Seront à vos côtés même dans la mort.....";
			break;
		//Guilds Castles
		case 2:
			mes "Bienvenue, membre de la Guilde ^5533FF" + GetGuildName(@GID) + "^000000. Nous serons à vos côtés où que vous soyez.";
			break;
		//Amatsu
		case 3:
			mes "Etes-vous venu d'un pays lointain pour étudier notre culture ? Ou faites-vous simplement du tourisme ?";
			mes "Dans tous les cas, pourquoi ne pas séjourner ici un moment ?";
			mes "L'air est toujours empli du parfum des fleurs sauvages.";
			break;
		//Louyang, Ayothaya
		case 4:
			mes "Avec tous les endroits couverts par les services Kafra, vous n'êtes jamais loin de chez vous.";
			break;
	}
	next;

	M_Menu:
		cleararray @K_Menu0$[0],"",7;
	switch(getarg(1)){
		//only Save & Storage
		case 1:
			setarray @K_Menu0$[0],"-Sauvegarder","-Utiliser le coffre","-Annuler";
			break;
		//only Storage
		case 2:
			setarray @K_Menu0$[0],"-Utiliser le coffre","-Annuler";
			break;
		//Common w/o teleport
		case 3: 
			setarray @K_Menu0$[0],"-Sauvegarder","-Utiliser le coffre","-Louer une charette","-Guide","-Vérification des autres informations","-Annuler";
			break;
		//Case 4 is Einbroch no tele message.
		//Common w/o save and teleport
		case 5:
			setarray @K_Menu0$[0],"-Utiliser le coffre","-Louer une charette","-Vérification des autres informations","-Guide","-Annuler";
			break;
		//Only storage and other info check
		case 6:
			setarray @K_Menu0$[0],"-Utiliser le coffre","-Guide","-Vérification des autres informations","-Annuler";
			break;
		//Common Kafra
		default:
			setarray @K_Menu0$[0],"-Sauvegarder","-Utiliser le coffre","-Utiliser le service de téléportation","-Louer une charette","-Guide","-Vérification des autres informations","-Annuler";
			break;
	}
	if(getarg(0)==2) {
	//Guilds Kafra (free Teleport, free Storage, Free Cart)
		cleararray @K_Menu0$[0],"",7;
		setarray @K_Menu0$[0],"-Utiliser le coffre","-Utiliser le coffre de Guilde","-Louer une charette","-Utiliser le service de téléportation","-Annuler";
	}
	menu 	@K_Menu0$[0],K_Menu0,@K_Menu0$[1],K_Menu1,@K_Menu0$[2],K_Menu2,
		@K_Menu0$[3],K_Menu3,@K_Menu0$[4],K_Menu4,@K_Menu0$[5],K_Menu5,
		@K_Menu0$[6],K_Menu6,@K_Menu0$[7],K_Menu7;
	K_Menu0:
		set @num,0;
		goto K_Menuf;
	K_Menu1:
		set @num,1;
		goto K_Menuf;
	K_Menu2:
		set @num,2;
		goto K_Menuf;
	K_Menu3:
		set @num,3;
		goto K_Menuf;
	K_Menu4:
		set @num,4;
		goto K_Menuf;
	K_Menu5:
		set @num,5;
		goto K_Menuf;
	K_Menu6:
		set @num,6;
		goto K_Menuf;
	K_Menu7:
		set @num,7;
	K_Menuf:
		if (@K_Menu0$[@num] == "-Sauvegarder") return;
		if (@K_Menu0$[@num] == "-Utiliser le coffre"){
			//Don't charge for a common Kafra Storage in your Castle
			if(getarg(0) == 2) callfunc "F_KafStor",2;
			else callfunc "F_KafStor",0;
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "-Utiliser le service de téléportation"){
			if(getarg(1) == 4){				//Check for Einbroch Tele notice
				mes "[Kafra]";
				mes "A cause de la ^FF0000Limitation des Moyens de Transport^000000, la Kafra Corporation ne peut pas fournir le service de téléportation dans la République de Schwartzwald.";
				next;
				mes "[Kafra]";
				mes "Nous vous prions d'utiliser le vaisseau volant à la place.";
                                mes "Merci de votre compréhension et de votre coopération.";
				next;
				goto M_Menu;
			}
			callfunc "F_KafTele",getarg(0);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "-Louer une charette"){
			if(callfunc("F_KafCart",getarg(0)) == 1) next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "-Guide"){
			callfunc "F_KafGuide";
			next;
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "-Vérification des autres informations"){
			callfunc "F_KafInfo",getarg(2);
			goto M_Menu;
		}
		if (@K_Menu0$[@num] == "-Annuler"){
			callfunc "F_KafEnd",getarg(0),0;
			end;
		}
		if (@K_Menu0$[@num] == "-Utiliser le coffre de Guilde"){
			callfunc "F_KafStor",1;
			next;
			goto M_Menu;
		}

}


// Storage Function =======================================================
function	script	F_KafStor	{
	if(getarg(0) == 1){
		if(guildopenstorage(0) == 1){
			mes "[Kafra]";
			mes "Je suis désolée mais un autre membre de votre Guilde est en train d'utiliser le coffre de Guilde. Je vous prie d'attendre qu'il ait fini.";
			close2;
			cutin "", 255;
			end;
		}
		cutin "", 255;
		close;
	}
	if(basicskillcheck() && getskilllv(1) < 6){
		mes "[Kafra]";
		mes "Je suis désolée mais vous devez maîtriser Basic Skills niveau 6 minimum si vous désirez utiliser le coffre.";
		return;
	}
	//don't charge for common storage in Castle
	if(getarg(0) != 2){
		//we'll use Free Ticket for Kafra Storage if available
		if(countitem(7059)){
			delitem 7059,1;
		} else {
			if(Zeny<40){
				mes "[Kafra]";
				mes "Chéri, vous n'avez pas assez d'argent. Il faut payer 40 Zenies pour accéder à votre coffre.";
				return;
			}
			set Zeny, Zeny-40;
			set RESRVPTS, RESRVPTS + (40/5);
		}
	}
	mes "[Kafra]";
	mes "Fermez cette fenêtre pour accéder à votre coffre.";
	mes "Nous espérons vous revoir bientôt.";

	callfunc("F_CheckKafCode");	//check your storage password, if set

	close2;
	openstorage;
	cutin "", 255;
	end;
}


// Teleport Function ==================================================
function	script	F_KafTele	{
	mes "[Kafra]";
	mes "Veuillez choisir votre destination.";
	next;

	menu 	@wrpC$[0],M_Wrp0, @wrpC$[1],M_Wrp1, @wrpC$[2],M_Wrp2, @wrpC$[3],M_Wrp3, 
		@wrpC$[4],M_Wrp4, @wrpC$[5],M_Wrp5, @wrpC$[6],M_Wrp6;

	M_Wrp0:
		set @num, 0;
		goto L_Warp;
	M_Wrp1:
		set @num, 1;
		goto L_Warp;
	M_Wrp2:
		set @num, 2;
		goto L_Warp;
	M_Wrp3:
		set @num, 3;
		goto L_Warp;
	M_Wrp4:
		set @num, 4;
		goto L_Warp;
	M_Wrp5:
		set @num, 5;
		goto L_Warp;
	M_Wrp6:
		set @num, 6;

	L_Warp:
		if (@wrpC$[@num] == "Annuler") return;
		// we'll use Free Ticket for Kafra Transportation if available
		if(countitem(7060)){
			delitem 7060,1;
		} else {
			if (Zeny<@wrpP[@num]){
				mes "[Kafra]";
				mes "Chéri, vous n'avez pas assez d'argent. Revérifiez vos fonds.";
				emotion e_cash;
				close2;
				cutin "", 255;
				end;
			}
			set Zeny, Zeny-@wrpP[@num];
			set RESRVPTS, RESRVPTS + (@wrpP[@num]/16);
		}
		if (@wrpD$[@num] == "Alberta") warp "alberta", 117, 56;
		if (@wrpD$[@num] == "Al De Baran") warp "aldebaran",143,110;
		if (@wrpD$[@num] == "Aldebaran") warp "aldebaran",143,110;
		if (@wrpD$[@num] == "Comodo") warp "comodo", 207, 144;
		if (@wrpD$[@num] == "Izlude") warp "izlude", 91, 105;
		if (@wrpD$[@num] == "Geffen") warp "geffen", 120, 39;
		if (@wrpD$[@num] == "Morroc") warp "morocc", 156, 46;
		if (@wrpD$[@num] == "Payon") warp "payon", 168, 103;
		if (@wrpD$[@num] == "Prontera") warp "prontera", 116, 72;
		if (@wrpD$[@num] == "Coal Mine(Dead Pit)") warp "mjolnir_02", 82, 347;
		if (@wrpD$[@num] == "Comodo Pharos Lighthouse") warp "cmd_fild07", 127, 134;
		if (@wrpD$[@num] == "Orc Dungeon") warp "gef_fild10", 52, 326;
		if (@wrpD$[@num] == "Umbala") warp "umbala", 130, 130;
		if (@wrpD$[@num] == "Yuno") warp "yuno", 157, 123;
		cutin "", 255;
		end;
}


// Cart Function ========================================================
function	script	F_KafCart	{
	if(baseClass != Job_Merchant){
		mes "[Kafra]";
		mes "Je suis désolée chéri. Le service de location de charettes est réservé aux Merchants, Blacksmiths, Whitesmiths, Alchemists et Creators.";
		return 1;
	}
	if(getskilllv(39)==0){
		mes "[Kafra]";
		mes "Je suis désolée mais vous avez besoin du Skill ^0000FFPushcart^000000 pour louer une charette.";
		return 1;
	}
	if(checkcart() == 1){
		mes "[Kafra]";
		mes "Excusez-moi... Mais vous avez déjà une charette";
		emotion e_swt;
		return 1;
	}
	//get Cart in Guild for free
	if(getarg(0) == 2) goto L_FreeCart;
	//use Free Ticket for the Cart Service if available
	if(countitem(7061)){
		delitem 7061,1;
		goto L_FreeCart;
	}
	mes "[Kafra]";
	mes "Le coût d'un Pushcart est de 800 Zenies. Voulez-vous louer une charette ?";
	next;
	menu "-Louer une charette.",-, "-Annuler.",M_End;

		if(Zeny<800){
			mes "[Kafra]";
			mes "Chéri, vous n'avez-pas assez d'argent. Vous avez besoin de 800 Zenies.";
			emotion e_cash;
			return 1;
		}
		set Zeny,Zeny-800;
		set RESRVPTS, RESRVPTS + 48;
	L_FreeCart:
		setcart;
		mes "[Kafra]";
		mes "Voici votre Puschcart";
		return 1;
	M_End:
		return 0;
}


// Pass Function ===============================================================
function	script	F_KafGuide	{

	mes "[Kafra]";
	mes "WIP...";
	return;
}

// Special Reserve Points Function ===========================================
function	script	F_KafInfo	{

	sM_Menu:
//Uncomment next line to block Kafra Storage Protection
//	if(getarg(0) == 0) menu "-Special Reserve Check",sM_ResChk, "-Kafra Locations",sM_KafLoc, "-Cancel",sM_End;
	if(getarg(0) == 0) menu "-Vérification de la réserve spéciale",sM_ResChk, "-Service de protection du coffre",sM_KafCode, "-Localisation des Kafras",sM_KafLoc, "-Annuler",sM_End;

	sM_ResChk:
		mes "[Kafra]";
		mes "Voici le montant actuel de vos Special Reserve Points.";
		mes "^0000ff"+RESRVPTS+"^000000.";
		next;
		mes "[Kafra]";
		mes "N'oubliez pas de continuer à utiliser les services Kafra comme le coffre ou la téléportation, afin de gagner plus de Special Reserve Points.";
		next;
		mes "[Kafra]";
		mes "Vous pouvez les échanger au Siège Social de la Kafra Corp. à Al De Baran contre des objets très utiles et des prix sympathiques.";
		next;
		if(getarg(0) == 1) return;
		goto sM_Menu;
	sM_KafLoc:
		mes "[Kafra]";
		mes "Les signaux clignotants sur votre mini-carte indiquent la localisation de toutes les agents Kafra dans cette ville.";
		viewpoint 1,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 1,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 1,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 1,@viewpX[3],@viewpY[3],4,0xFF00FF;
		next;
		viewpoint 2,@viewpX[0],@viewpY[0],1,0xFF00FF;
		viewpoint 2,@viewpX[1],@viewpY[1],2,0xFF00FF;
		viewpoint 2,@viewpX[2],@viewpY[2],3,0xFF00FF;
		viewpoint 2,@viewpX[3],@viewpY[3],4,0xFF00FF;
		goto sM_Menu;

	sM_KafCode:
		callfunc("F_SetKafCode");

	sM_End:
		return;
}


// End Function =====================================================
//  arg(0): used to determine what message to display.
//  arg(1): used to determine if save message is displayed.
//===================================================================
function	script	F_KafEnd	{
	mes "[Kafra]";
	if(getarg(1)==1) mes "Votre point de Respawn a été enregistré.";		// only shown when a player uses save
	if(getarg(0)!=1) mes "Merci d'avoir utilisé les services Kafra. Nous espérons vous revoir bientôt.";
	if(getarg(0)==1) mes "Nous, Kafra Corporation.... Serons à vos côtés.... Toujours.... Toujours... Donc.... ne nous oubliez pas.....";
	close2;
	cutin "", 255;
	emotion e_thx;
	close;
	end;
}

// Check Storage Password Function ====================
function	script	F_CheckKafCode	{
	if(#kafra_code==0) return;
	mes "Entrez le mot de passe de votre coffre :";
	set @code_,0;
	input @code_;
	if(@code_ != #kafra_code-getcharid(3)-1337) {
		dispbottom "Mauvais mot de passe !";
		close2;
		cutin "",255;
		end;
	}
	set @kafcode_try,0;
	set @code_,0;
	return;
}

// Set / Change / Clear Storage Password Function ====================
function	script	F_SetKafCode	{
	mes "[Kafra]";
	if(#kafra_code) {
		mes "Votre coffre est protégé par un mot de passe. Que désirez-vous faire ?";
		next;
		menu "Changer l'ancien mot de passe->5000 Zenies",-,
		"Effacer l'ancien mot de passe->1000 Zenies",M_CLEAR,
		"Cancel",M_END;
	} else {
		mes "Les services Kafras sont fiers de vous présenter un nouveau service :";
		mes "La protection supplémentaire du coffre par mot de passe.";
		next;
		menu "Choisir un nouveau mot de passe->5000 Zenies",M_SET,
		"Annuler",M_END;
	}

	mes "[Kafra]";
	mes "En premier lieu, entrez votre ^0000FFancien mot de passe^000000.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0 || @code != #kafra_code-getcharid(3)-1337) {
		mes "Mauvais mot de passe. Vous ne pouvez pas en choisir un nouveau.";
		emotion e_hmm;
		goto M_END;
	}
	next;

M_SET:
	mes "[Kafra]";
	mes "Maintenant entrez votre ^FF0000nouveau mot de passe^000000 pour protéger votre coffre des voleurs.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "Le mot de passe n'a pas été changé.";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes "[Kafra]";
	if(Zeny < 5000) goto L_ZENY;
	set Zeny,Zeny-5000;
	set RESRVPTS, RESRVPTS + (5000/50);

	set #kafra_code,@code+getcharid(3)+1337;
	mes "Vous avez protégé votre coffre avec un mot de passe secret.";
	mes "Merci d'avoir utilisé les services Kafra.";
	emotion e_thx;
	goto M_END;	

M_CLEAR:
	mes "[Kafra]";
	mes "Veuillez entrer votre mot de passe avant son effacement.";
	set @code,callfunc("F_EntKafCode");
	if(@code==0) {
		mes "Le mot de passe n'a pas été effacé.";
		emotion e_hmm;
		goto M_END;
	}
	next;
	mes "[Kafra]";
	if(Zeny < 1000) goto L_ZENY;
	set Zeny,Zeny-1000;
	set RESRVPTS, RESRVPTS + (1000/50);
	if(@code == #kafra_code-getcharid(3)-1337) {
		set #kafra_code,0;
		mes "Vous avez bien effacé le mot de passe de votre coffre.";
		mes "Merci d'avoir utilisé les services Kafra.";
		emotion e_thx;
	} else {
		mes "Mauvais mot de passe. Nous ne vous rembourserons pas vos 1000 Zenies.";
		mes "Veuillez entrer le bon mot de passe la prochaine fois.";
		emotion e_sry;
	}
	goto M_END;	

L_ZENY:
	mes "Vous n'avez pas assez d'argent.";
	emotion e_cash;
M_END:
	close2;
	cutin "",255;
	end;
}

// Basic Password Validation Function ====================
function	script	F_EntKafCode	{
	mes "Entrez un nombre entre 1000 et 10000000 :";
	set @code_,0;
	set @kafcode_try,@kafcode_try+1;
	if(@kafcode_try>10) {
		set @kafcode_try,0;
		logmes "Hack : tentative de forcage du mot de passe du coffre.";
	}
	input @code_;
	if(@code_<1000) {
		mes "Vous ne devriez pas utiliser un mot de passe aussi court.";
		return 0;
	}
	if(@code_>10000000) {
		mes "Vous ne pouvez pas utiliser un mot de passe aussi long.";
		return 0;
	}
	return @code_;
}

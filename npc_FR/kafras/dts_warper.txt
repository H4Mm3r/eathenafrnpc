//===== eAthena Script ======================================= 
//= Dungeon Teleport Service
//===== By: ================================================== 
//= Evera
//===== Current Version: ===================================== 
//= 2.3c
//===== Compatible With: ===================================== 
//= eAthena
//===== Description: =========================================
//= Cool Event Corp NPCs vs. Kafra Dungeon Teleport Service and voting system.
//= This is a special event on official servers where there's a
//= big competition between Kafra Corp. and Cool Event Corp.
//= To warp to selected levels of certain dungeons.
//= Also includes Cool Event Corp HQ NPCs.
//===== Variables: ===========================================
//=Server - 	$dtsvote		= Variable for # of votes
//=					= Positive = Cool Corp winning
//=					= Negative = Kafra Corp winning
//=		$dts			= Variable for DTS status
//=					= (1<<0) Election
//=					= (1<<1) Cool Corp Enabled
//=					= (1<<2) Kafra Enabled
//=					= (1<<3) No winner last election
//=					= (1<<4) Cool Corp won last election
//=					= (1<<5) Kafra won last election
//=		$dtsday			= Week count for dts vote count, used for resetting players' votes
//=Character -	dtseligible 		= eligibility status, 0 not eligible yet, 1 eligible.
//=					= $dtsday used when MISC_QUEST|128, and signifies that person already voted.
//=		MISC_QUEST		= |128 = eligible and voted.
//=NPC Func. -	arg(0) 			= 0, Cool Event Corp Voting Staff; 1, Kafra Corp Voting Staff
//=		arg(1)			= Kafra only, Cool Corp script does not use. Changes illus for different sprites.
//=					= 0 = 4_f_kafra6 (yellow ponytail, classic outfit, spr 112)
//=					= 1 = 4_f_kafra5 (pink hair, classic outfit, spr 113)
//=					= 2 = 4_f_kafra4 (orange short hair, classic outfit, spr 114)
//=					= 3 = 4_f_kafra3 (brown bangs, classic outfit, spr 115)
//=					= 4 = 4_f_kafra2 (brown ponytale, classic outfit, spr 116)
//=					= 5 = 4_f_kafra1 (blue hair, classic outfit, spr 117)
//=					= 6 = 4_f_agentkafra (purp hair, black outfit, spr 859)
//=					= 7 = 4_f_kafra8 (blue hair, brown outfit, spr 860)
//=					= 8 = 4_f_kafra9 (red hair, brown outfit, spr 861)
//=Temporary - 	@dtstemp		= Temporary buffer, used in GM vote-fixing
//=		@dtstemps$		= Temporary buffer, like above, but a string.
//=		$@dtstempg		= Temporary buffer, but global
//===== Additional Comments: ================================= 
//= Some unofficial translations, some unofficial dungeon warps
//= Also includes unofficial GM-enabled tweaking for script
//= Includes NPCs in Cool Event Corp headquarters
//= If new locations are found of this NPC, please report on forums
//= http://www.eathena.ws/board/index.php?showforum=106
//= Base level 60 required to vote
//= Traduit pour Agora-Online par Arkana
//===== Version History: ====================================
//= 1.0	Initial release [Evera]
//= 1.1	SVN release, removed from major town (found true info after research),
//=	put more arg(2) parameters, fixed a few bugs [Evera]
//= 1.2 Mushed some variables together, optimized a bit [Evera]
//= 1.3 Removed selfconfig [Evera]
//= 1.4 Changed global variables to read from 3 global variables, updated names,
//= 	fixed array bug [Evera]
//= 1.5 Mushed voting varialbes into 1 variable. [Evera]
//= 1.6 Removed Duplicates [Silent]
//= 1.7	Fixed bug of DTS_Admin not running by itself [Evera]
//= 1.8	Changed requirement to lvl 60 [Evera]
//= 1.9 Removed global eligibility option [Evera]
//= 2.0 Fixed zeny bug [Evera]
//= 2.1 Fixed headers with and updated with newer information [Evera]
//= 2.2 Changed dtseligible 2 to MISC_QUEST|128, to clean up variable usage. [Evera]
//=	Also added F_ClearGarbage function to the scripts. [Evera]
//= 2.3	Changed dtseligible to be $dtsday when MISC_QUEST|128
//=	Added $dtsday for vote session counter
//=	Fixed bug that involved having both warpers enabled at the same time. [Evera]
//=	New $dtsday system and bug fix[Evera]
//= 2.3a fixed 2 bugs with comparision [Lupus]
//= 2.3b corrected Glast warp coords [Lupus] 2.3c Bailand -> Bayalan
//============================================================ 

//Yuno
yuno,153,191,4	script	Cool Event Voting Staff::CoolEventVotingStaff01	874,{
	callfunc "F_DTS_Warp",0,0;
}
yuno,162,191,6	script	Kafra Voting Staff#01	861,{
	callfunc "F_DTS_Warp",1,8;
}
//Lighthalzen
lighthalzen,154,60,6	script	Cool Event Voting Staff::CoolEventVotingStaff02	874,{
	callfunc "F_DTS_Warp",0,0;
}
//Prontera
prontera,147,125,4	script	Cool Event Voting Staff::CoolEventVotingStaff03	874,{
	callfunc "F_DTS_Warp",0,0;
}
prontera,164,125,6	script	Kafra Voting Staff#02	115,{
	callfunc "F_DTS_Warp",1,3;
}

//Cool Event Staff Headquarters NPCs
//Kudiuu (Maintenance Guy)
lhz_in02,20,274,6	script	Maintenance Guy	851,{
	mes "[Kudiuu]";
	mes "Mon Dieu...!";
	mes "Cette salle ne sera donc jamais propre ?!";
	mes "*Tousse* Il y a tellement de poussière ici, c'est un vrai nid à microbes !";
	close;
}
//Cesuna (Zondaman at desk)
lhz_in02,36,284,1	script	Cool Event Staff#01	874,{
	mes "[Cesuna]";
	mes "Ack! Je suis totallement submergé de travail !";
        mes "Mais je n'y toucherai pas.";
        mes "C'est ça !";
	mes "J'ai vraiment besoin d'une pause.";
	next;
	mes "[Cesuna]";
	mes "*Soupire*";
	mes "Je me demande se Saera accepterait de sortir avec moi ?";
	mes "Ce serait bien~";
	close;
}
//Jellarin (Event planner)
lhz_in02,40,279,3	script	Event Planner	833,{
	mes "[Jellarin]";
	mes "Je n'aime pas ça.";
	mes "Mais je n'aime pas cette idée non-plus.";
        mes "Que pourais-je faire comme nouvel Event ?";
	next;
	mes "[Jellarin]";
	mes "Je besoin de quelque chose de grand, quelque chose qui fera trembler le monde, quelque chose d'actuel, mais quoi ?";
	mes "Hé, vous n'avez pas une idée ?";
	close;
}
//Baoto (Cool Event Manager)
lhz_in02,110,283,5	script	Cool Event Manager	853,{
	mes "[Baoto]";
	mes "Hmmm...";
	mes "Il semble y avoir une trop bonne ambiance entre les employés récemment.";
        mes "Ca n'est pas bon du tout...";
	next;
	mes "[Baoto]";
	mes "On dirait bien que je vais devoir claquer du fouet plus fort et plus souvent.";
        mes "Ha ha ha ha !";
	close;
}
//Saera (Secretary), contains GM menu
lhz_in02,36,274,3	script	Cool Event Staff#02	831,{
	callfunc "F_ClearGarbage";				//Clear outdated, unused variables
	if($dts == 0){
		set $dts,$dts|(1<<0);				//Sets NPCs to election mode if first time running
		set $dts,$dts|(1<<3);
	}
	if($dts&(1<<1) && $dts&(1<<2) || $dts&(1<<0) && $dts&(1<<1) || $dts&(1<<0) && $dts&(1<<2)){
		if($dts&(1<<0)) set $dts,$dts&~(1<<0);
		if($dts&(1<<1)) set $dts,$dts&~(1<<1);
		if($dts&(1<<2)) set $dts,$dts&~(1<<2);
		set $dts,$dts|(1<<0);
	}
	if($dts&(1<<4) && $dts&(1<<5) || $dts&(1<<3) && $dts&(1<<4) || $dts&(1<<3) && $dts&(1<<5)){
		if($dts&(1<<3)) set $dts,$dts&~(1<<3);
		if($dts&(1<<4)) set $dts,$dts&~(1<<4);
		if($dts&(1<<5)) set $dts,$dts&~(1<<5);
		set $dts,$dts|(1<<3);
	}
	mes "[Saera]";
	mes "Bienvenue au Siège Social temporaire de la Cool Event Corporation.";
        mes "Comment puis-je vous aider ?";
	next;
	if(getgmlevel()>=40){ 						//Unofficial Text, GM-configurable settings for Event (GM level above 40 required)
		mes "[Saera]";
		mes "Comment se fait-il que je ne vous ai pas déjà remarqué...";
		mes strcharinfo(0)+" que désirez-vous faire ?";
		next;
		menu "Mettre en place un vote",Lfixvote,
		"Désigner le téléporteur actuel",Lsettele,
		"Désigner le gagnant des dernières élections.",Lsetlast,
		"Vérifier manuellement le vote",Lmanuvotecheck,
		"Le menu normal, s'il vous-plaît",-;
		mes "[Saera]";
		mes "Ok";
		next;
	}
	menu "Siège Social temporaire ?",Ltemphead,
	"Voter",Lvoting,"Non merci.",Lnothx;

	Ltemphead:
		mes "[Saera]";
		mes "Notre Siège Social est actuellement en reconstruction, nous avons donc basé nos opérations ici pendant le temps nécéssaire.";
		close;

	Lvoting:
		mes "[Saera]";
		mes "En ce moment; Kafra Corporation et Cool Event Corporation sont en train de travailler sur un programme de collaboration qui fournira des services de téléportation directe vers les donjons.";
		next;
		mes "[Saera]";
		mes "Mais à cause de raisons techniques, les deux compagnies de peuvent pas proposer des services de téléportation vers les mêmes donjons.";
                mes "Donc, nous demandons à nos chers clients de choisir la compagnie de leur choix.";
		next;
		if((dtseligible == 0 || dtseligible == 3) && baselevel >= 60 && MISC_QUEST&128 == 0) set dtseligible,1;
		//Clear previous var of dtseligible,3 being tried for eligibility already
		if(dtseligible != $dtsday && baselevel >= 60 && MISC_QUEST&128){
			set MISC_QUEST,MISC_QUEST&~128;
			set dtseligible,1;
		}
		switch(dtseligible){
			default:
				mes "[Saera]";
				mes "Seul un nombre limité d'électeurs sera choisi, vous pouvez donc vérifier si vous avez le droit de voter dans les Sièges Sociaux des deux compagnies.";
				mes "Merci de votre soutient~";
				close;
				break;
			case 1:
				mes "[Saera]";
				mes "Il semble que vous soyez en règle pour voter, nous vous prions donc de voter auprès des Voting Staff représentant les deux compagnies.";
                                mes "Merci~";
				close;
				break;
			case 2:
				mes "[Saera]";
				mes "Vous avez l'autorisation et avez déjà voté.";
                                mes "Merci de votre participation.";
				close;
				break;
		}

	Lnothx:
		mes "[Saera]";
		mes "Merci.";
		mes "Bonne journée.";
		close;
									//GM options start
	Lfixvote:							//Fix Vote
		mes "[Saera]";
		mes "Umm, bien sûr...";
		if($dtsv > 0) mes "Cool Event Corp l'emporte actuellement avec"+$dtsv+" votes.";
		if($dtsv < 0) mes "Kafra Corp l'emporte actuellement avec"+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Le vote est à égalité.";
		mes "A qui souhaiteriez-vous accorder un vote ?";
		next;
		menu "Kafra",-,"Cool Event Corp",Lfixcool;

	Lfixkafra:							//Fixing vote for Kafra
		mes "[Saera]";
		if($dtsv > 0) mes "Cool Event Corp l'emporte actuellement avec"+$dtsv+" votes.";
		if($dtsv < 0) mes "Kafra Corp l'emporte actuellement avec"+$dtsv*-1+" votes.";	//multiplied by -1 because var is negative
		if($dtsv == 0) mes "Le vote est à égalité.";
		mes "Vous pouvez ajouter de nouveaux votes à Kafra Corp.";
		input @dtstemp;					//Set buffer for Kafra vote
		next;
		mes "[Saera]";
		mes "Vous avez donné "+@dtstemp+" votes à Kafra.";
		if($dtsv > 0) mes "Cool Event Corp l'emporte actuellement avec"+$dtsv+" votes.";
		if($dtsv < 0) mes "Kafra Corp l'emporte actuellement avec"+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Le vote est à égalité";
		mes "Etes-vous certain de vouloir faire ce changement ?";
		next;
		menu "Oui",-,"Non",Lnothx;
		set $dtsv,$dtsv-@dtstemp;				//Set buffer to Kafra vote
		mes "[Saera]";
		mes "Ok, vous avez arrangé le vote";
		mes "pour Kafra Corp.";
		if($dtsv > 0) mes "Cool Event Corp l'emporte actuellement avec"+$dtsv+" votes.";
		if($dtsv < 0) mes "Kafra Corp l'emporte actuellement avec"+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Le vote est à égalité.";
		close;

	Lfixcool:						//Fixing vote for cool corp
		mes "[Saera]";
		mes "Laissez-moi chercher mes notes...";
		if($dtsv > 0) mes "Cool Event Corp l'emporte actuellement avec"+$dtsv+" votes.";
		if($dtsv < 0) mes "Kafra Corp l'emporte actuellement avec"+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Le vote est à égalité.";
		mes "Vous pouvez ajouter de nouveaux votes pour Cool Event Corp.";
		input @dtstemp;					//Set buffer for Cool vote
		next;
		mes "[Saera]";
		mes "Vous avez ajouté "+@dtstemp+" votes pour Cool Event Corp";
		if($dtsv > 0) mes "Cool Event Corp l'emporte actuellement avec"+$dtsv+" votes.";
		if($dtsv < 0) mes "Kafra Corp l'emporte actuellement avec"+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Le vote est à égalité.";
		mes "Etes-vous certain de vouloir faire ce changement ?";
		next;
		menu "Oui",-,"Non",Lnothx;			//Confirmaiton
		set $dtsv,@dtsv+@dtstemp;			//Set buffer to Cool vote
		mes "[Saera]";
		mes "Ok, vous avez arrangé le vote pour Cool Event Corp.";
		if($dtsv > 0) mes "Cool Event Corp l'emporte actuellement avec"+$dtsv+" votes.";
		if($dtsv < 0) mes "Kafra Corp l'emporte actuellement avec"+$dtsv*-1+" votes.";
		if($dtsv == 0) mes "Le vote est à égalité.";
		close;

	Lsettele:							//Set current DTS tele
		mes "[Saera]";
		mes "Entrez ''Kafra'' pour accorder les warps à Kafra.";
                mes "''Cool'' pour accorder les warps à Cool Event Corp.";
                mes "''Election'' pour lancer des élections.";
                mes "ou ''Annuler'' pour annuler.";
		if($dts&(1<<0)) mes "Les élections sont en cours.";
		if($dts&(1<<1)) mes "Cool Event Corp est actullement le fournisseur de warps vers les donjons.";
		if($dts&(1<<2)) mes "Kafra est actullement le fournisseur de warps vers les donjons.";
		next;
		input @dtstemps$;					//Typed in choices to prevent mistakes
		mes "Laissez-moi trouver mes notes...";
		next;
		if(@dtstemps$ == "Election" || @dtstemps$ == "election"){
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);			//Removes previous winner
				set $dts,$dts|(1<<0);			//Changes status to election
			}
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<0);
			}
			mes "[Saera]";
			mes "Mise en place des élections.";
			close;
		}
		if(@dtstemps$ == "Cool" || @dtstemps$ == "cool"){
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<1);
			}
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<1);
			}
			mes "[Saera]";
			mes "Cool Event Corp est maintenant le fournisseur de warps vers les donjons.";
			close;
		}
		if(@dtstemps$ == "Kafra" || @dtstemps$ == "kafra"){
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);
				set $dts,$dts|(1<<2);
			}
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<2);
			}
			mes "[Saera]";
			mes "Kafra est maintenant le fournisseur de warps vers les donjons.";
			close;
		}
		if(@dtstemps$ == "Annuler" || @dtstemps$ == "annuler") goto Lnothx;
		mes "[Saera]";
		mes "Veuillez entrer un nom correct pour les élections.";
		next;
		goto Lsettele;

	Lsetlast:							//Set last winner
		mes "[Saera]";
		mes "Entrez ''Kafra'' pour afficher Kafra Corp en tant que vainqueur.";
		mes "Entrez ''Cool'' pour afficher Cool Event Corp en tant que vainqueur.";
		mes "Entrez ''Rien'' pour n'afficher aucun vainqueur.";
		mes "ou ''Annuler'' pour annuler.";
		if($dts&(1<<3)) mes "Il n'y a pas encore eu de vainqueurs.";
		if($dts&(1<<4)) mes "Cool Event Corp est le dernier gagnant.";
		if($dts&(1<<5)) mes "Kafra Corp est le dernier gagnant.";
		next;
		input @dtstemps$;
		mes "[Saera]";
		mes "Laissez-moi trouver mes notes....";
		next;
		if(@dtstemps$ == "None" || @dtstemps$ == "none"){
			if($dts&(1<<4)){
				set $dts,$dts&~(1<<4);			//removes last winner
				set $dts,$dts|(1<<3);			//sets current last winner
			}
			if($dts&(1<<5)){
				set $dts,$dts&~(1<<5);
				set $dts,$dts|(1<<3);
			}
			mes "[Saera]";
			mes "Aucun vainqueur ne sera affiché.";
			close;
		}
		if(@dtstemps$ == "Cool" || @dtstemps$ == "cool"){
			if($dts&(1<<3)){
				set $dts,$dts&~(1<<3);
				set $dts,$dts|(1<<4);
			}
			if($dts&(1<<5)){
				set $dts,$dts&~(1<<5);
				set $dts,$dts|(1<<4);
			}
			mes "[Saera]";
			mes "Cool Event Corp. est affiché en tant que vainqueur.";
			close;
		}
		if(@dtstemps$ == "Kafra" || @dtstemps$ == "kafra"){
			if($dts&(1<<3)){
				set $dts,$dts&~(1<<3);
				set $dts,$dts|(1<<5);
			}
			if($dts&(1<<4)){
				set $dts,$dts&~(1<<4);
				set $dts,$dts|(1<<5);
			}
			mes "[Saera]";
			mes "Kafra Corp. est affiché en tant que vainqueur.";
			close;
		}
		if(@dtstemps$ == "Cancel" || @dtstemps$ == "cancel") goto Lnothx;
		mes "[Saera]";
		mes "Veuillez entrez le nom correct de l'ancien vainqueur.";
		next;
		goto Lsetlast;

	Lmanuvotecheck:
		mes "[Saera]";
		mes "Etes vous certain de vouloir relancer la vérification du vote ?";
		next;
		menu "Oui",Lmanuvoteyes,"Non",Lnothx;

	Lmanuvoteyes:
		if($dtsday<4 || $dtsday>=100) set $dtsday,4;
		else set $dtsday,$dtsday+1;
		if($dts&(1<<1) && $dts&(1<<2) || $dts&(1<<0) && $dts&(1<<1) || $dts&(1<<0) && $dts&(1<<2)){
			if($dts&(1<<0)) set $dts,$dts&~(1<<0);
			if($dts&(1<<1)) set $dts,$dts&~(1<<1);
			if($dts&(1<<2)) set $dts,$dts&~(1<<2);
			set $dts,$dts|(1<<0);
		}
		if($dts&(1<<4) && $dts&(1<<5) || $dts&(1<<3) && $dts&(1<<4) || $dts&(1<<3) && $dts&(1<<5)){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			set $dts,$dts|(1<<3);
		}
		if($dtsv == 0){
			set $@dtstemp,rand(1,2);
			if($@dtstemp == 1) set $dtsv,$dtsv+100;
			else set $dtsv,$dtsv-100;
		}
		if($dtsv > 0){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);			//removes last winner varialbes
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);				//removes winner variable
				set $dts,$dts|(1<<3);				//sets last winner varialbe
				set $dts,$dts|(1<<1);				//sets current winner variable
			}
			if($dts&(1<<1)) set $dts,$dts|(1<<4);
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<1);
				set $dts,$dts|(1<<5);
			}
			set $dtsv,0;
		}
		else{
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<3);
			}
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<4);
			}
			if($dts&(1<<2)) set $dts,$dts|(1<<5);
			set $dtsv,0;
		}
		mes "[Saera]";
		mes "Vérification du vote relancée.";
		if($dts&(1<<0)) mes "L'élection est en cours.";
		if($dts&(1<<1)) mes "Cool Event Corp. est actuellement le fournisseur de warps vers les donjons.";
		if($dts&(1<<2)) mes "Kafra Corp. est actuellement le fournisseur de warps vers les donjons.";
		close;
}

//Function for Voting Staff NPC
function	script	F_DTS_Warp	{
	callfunc "F_ClearGarbage"; //Clear outdated, unused variables
	if($dts == 0){ 						//Sets NPCs to election mode if first time running
		set $dts,$dts|(1<<0);
		set $dts,$dts|(1<<3);
	}
	if((dtseligible == 0 || dtseligible == 3) && baselevel >= 60 && MISC_QUEST&128 == 0) set dtseligible,1;	//Clear previous var of dtseligible,3 being tried for eligibility already
	if(dtseligible != $dtsday && baselevel >= 60 && MISC_QUEST&128){
		set MISC_QUEST,MISC_QUEST&~128;
		set dtseligible,1;
	}
	switch(getarg(0)){
	case 0:
		cutin "zonda_01",2;
		mes "[Cool Event Corp. Voting Staff]";
		mes "Bonjour ! N'oubliez pas de faire entendre votre voix en votant pour Cool Event Corp. et Kafra Corp. pour le contrôle du service de téléportation vers les donjons !";
		break;
	case 1:
		switch(getarg(1)){
		case 0: cutin "kafra_06",2; break;			//Finding arguments to find which illust to use
		case 1: cutin "kafra_05",2; break;			//Used Red hair "kafra_09" for black outfit kafra
		case 2: cutin "kafra_04",2; break;			//because I couldn't find a suitable illust
		case 3: cutin "kafra_03",2; break;
		case 4: cutin "kafra_02",2; break;
		case 5: cutin "kafra_01",2; break;
		case 6: cutin "kafra_09",2; break;
		case 7: cutin "kafra_08",2; break;
		case 8: cutin "kafra_09",2; break;
		default: cutin "kafra_09",2; break;
		}
		mes "[Kafra Voting Staff]";
		mes "Salutations, aventurier.";
		mes "Comme vous le savez peut-être, nous tenons des élections pour fournir le service de téléportation vers les donjons.";
                mes "Puis-je vous aider ?";
		break;
	}
	next;
	menu "Raisons de l'élection",Lelection,"Voter",Lvote,
	"Utiliser le service de téléportation",Lteleport,"Annuler",Lcancel;

	Lelection: 							//Reason for election explanation
		switch(getarg(0)){
		case 0:
			mes "[Cool Event Corp. Voting Staff]";
			mes "Cool Event Corp. a prévu de fournir un nouveau service de téléportation vers les donjons à ses clients, un service que la Kafra Corp. n'offre pas.";
                        mes "Cependant...";
			next;
			mes "[Cool Event Corp. Voting Staff]";
			mes "Kafra Corp., qui possède déjà le monopole de la téléportation publique, prévoit en fait d'offrir un service simillaire.";
			next;
			mes "[Cool Event Corp. Voting Staff]";
			mes "A cause de limitations technologiques, une seule compagnie pourra être choisie comme fournisseur de ce service de téléportation vers les Donjons.";
			mes "D'où le fait que laissions nos clients décider à travers ces élections.";
			next;
			mes "[Cool Event Corp. Voting Staff]";
			mes "Plusieurs élections seront tenues, afin que nos clients puissent tester les services spéciaux de chaque compagnie eux-mêmes.";
			mes "Cependant, n'oubliez pas que vous devez avoir le droit de voter.";
			next;
			mes "[Cool Event Corp. Voting Staff]";
			mes "Pour plus de détails sur le droit de vote, veuillez vous rendre à notre Siège Social à Lighthalzen, situé dans la République de Schwartzwald.";
			mes "Merci de nous avoir accordé de votre temps.";
			break;
		case 1:
			mes "[Kafra Voting Staff]";
			mes "Cool Event Corp. et Kafra Corp. ont toutes les deux prévues de fournir un service de téléportation vers les donjons.";
			next;
			mes "[Kafra Voting Staff]";
			mes "Mais à cause de limitations technologiques, une seule compagnie pourra fournir la téléportation vers les donjons.";
                        mes "Donc les deux compagnies se sont entendues pour faire des élections spéciales.";
			next;
			mes "[Kafra Voting Staff]";
			mes "Chaque compagnie a ses polices et ses garanties quant au service de téléportation vers les donjons, et dans ces élections, le client aura la décision finale et choisira ce qui est le mieux pour lui.";
			next;
			mes "[Kafra Voting Staff]";
			mes "Pour le moment, le service de téléportation sera fourni pendant plusieurs périodes d'essai.";
                        mes "De cette manière, les clients pourront voir les bienfaits de chaque compagnie et faire leur décision finale.";
			next;
			mes "[Kafra Voting Staff]";
			mes "Si vous le pouvez, je vous prie de voter à chaque élection pour décider quelle compagnie fournira le service de téléportation vers les donjons pour la dernière période d'essai.";
			mes "Merci de votre soutient~";
			break;
		}
		goto Lend;

	Lvote: //If you clicked you wanted to vote
		if(dtseligible == 1) goto Leligible; 			//Var check if eligible

	Lnoteligible: 							//Text displayed if wanted to vote, but not eligible/voted
		switch(getarg(0)){
		case 0:
			mes "[Cool Event Corp. Voting Staff]";
			mes "Je suis désolé mais vous ne pouvez pas voter cette fois.";
			mes "Vous devriez visiter notre Siège Social à Lighthalzen pour plus d'informations sur l'acquisition du droit de vote.";
                        mes "Merci de nous avoir accordé de votre temps.";
			break;
		case 1:
			mes "[Kafra Voting Staff]";
			mes "Je suis désolée, mais vous ne pouvez pas voter.";
                        mes "Pour plus de détails sur le droit de vote, je vous invite à visiter notre Siège Social à Al De Baran.";
			mes "Merci et bonne journée.";
			break;
		}
		goto Lend;

	Leligible: 							//Text displayed if eligible to vote
		if(MISC_QUEST&128 && $dtsday == dtseligible) goto Lnoteligible; //Var check if voted
		switch(getarg(0)){
		case 0:
			mes "[Cool Event Corp. Voting Staff]"; 		//Unofficial text
			mes "Vos papiers indiquent que vous avez le droit de voter pour le service de téléportation vers les donjons.";
                        mes "Pour qui désirez-vous voter ?";
			break;
		case 1:
			mes "[Kafra Voting Staff]";
			mes "Il semble que vous soyez autorisé à voter pour le service de téléportation vers les donjons.";
			mes "Pour qui désirez-vous voter ?";		//End Unofficial text
			break;
		}
		next;
		menu "Cool Event Corp.",Lvotecool,"Kafra Corp.",Lvotekafra;
	
	Lvotecool:
		set $dtsv,$dtsv+1;				//Adds 1 to cool count
		set MISC_QUEST,MISC_QUEST|128;				//Sets var so that you can't vote over and over
		set dtseligible,$dtsday;
		switch(getarg(0)){
		case 0:
			mes "[Cool Event Corp. Voting Staff]";
			mes "Merci d'avoir voté pour nous.";
			mes "Votre vote a été compté, et merci de votre soutiens.";
			mes "Merci et bonne journée.";
			break;
		case 1:
			mes "[Kafra Voting Staff]";
			mes "Nous vous remercions pour votre vote.";
			mes "Votre opinion est très importante et a été comptée pour Cool Event Corp.";
			break;
		}
		goto Lend;

	Lvotekafra:
		setd $dtsv,$dtsv-1; 				//Adds 1 to kafra count
		set MISC_QUEST,MISC_QUEST|128; 			//Sets var to prevent cheating
		set dtseligible,$dtsday;
		switch(getarg(0)){
		case 0:
			mes "[Cool Event Corp. Voting Staff]";		//Unofficial Text
			mes "Merci d'avoir exprimé votre opinion.";
			mes "Votre vote pour Kafra Corp. a été compté.";
                        mes "Merci et bonne journée.";
			break;
		case 1:
			mes "[Kafra Voting Staff]";
			mes "Votre vote a été compté.";
                        mes "Nous vous remercions beaucoup de votre soutiens, et nous espérons que nous satisferons tous vos besoins d'aventures et vos attentes d'excellence.";
			break;
		}
		goto Lend;						//End Unofficial text

	Lteleport: 							//Selected Teleport
		switch(getarg(0)){
		case 0:
			if($dts&(1<<0)){
				mes "[Cool Event Corp. Voting Staff]";
				mes "Je suis désolé mais le service de téléportation vers les donjons n'est pas disponnible pendant les élections et sera réactivé après l'annonce des résultats.";
				mes "Merci et bonne journée.";
				goto Lend;
			}
			if($dts&(1<<1)) goto Lteleenabled;
			mes "[Cool Event Corp. Voting Staff]";		//Text if Kafra won DTS elect.
			mes "Je suis désolé mais Cool Event Corp. n'offre pas le service de téléportation vers les donjons, à cause des résultats du dernier vote.";
                        mes "S'il vous plaît, votez pour nous la prochaine fois. Bonne journée~";
			break;
		case 1:
			if($dts&(1<<0)){
				mes "[Kafra Voting Staff]";
				mes "Désolée mais les élections pour le service de téléportation vers les donjons est en cours.";
                                mes "Nous ne pouvons pas vous en faire profiter avant l'annonce des résultats.";
				goto Lend;
			}
			if($dts&(1<<2)) goto Lteleenabled;
			mes "[Kafra Voting Staff]";
			mes "Nous sommes désolés mais Kafra Corp. n'offre pas le service de téléportation vers les donjons, à cause des résultats du dernier vote.";
			mes "S'il vous plaît, votez pour Kafra Corp. la prochaine fois~";
			break;
		}
		goto Lend;

	Lteleenabled: 							//Shows DTS tele selections
	cleararray @dtswarpmap$[0],"",getarraysize(@dtswarpmap$);
	cleararray @dtswarp$[0],"",getarraysize(@dtswarp$);
	switch(getarg(0)){
	case 0:
		mes "[Cool Event Corp. Voting Staff]";
		mes "Veuillez garder en mémoire que nous n'acceptons pas les Free Warp Tickets et n'offrons aucune Special Reserve Point pour ce service.";
                mes "Maintenant, vous pouvez choisir votre destination.";
		setarray @dtswarpmap$[0],"Byalan, Level 4","Clock Tower, Basement 3";
		setarray @dtswarp$[0],@dtswarpmap$[0]+" -> 4,000z",
		@dtswarpmap$[1]+" -> 4,000z","Cancel";
		if($dts&(1<<4)){ 
			set @dtswarpmap$[2],"Glastheim Entrance";
			setarray @dtswarp$[2],@dtswarpmap$[2]+" -> 4,000z","Cancel";
		}
		break;
	case 1:
		mes "[Kafra Voting Staff]";
		mes "Merci d'utiliser le service de téléportation vers les donjons.";
		mes "Veuillez garder en mémoire que nous n'acceptions pas les Free Warp Tickets et n'offrons aucun Special Reserve Point pour ce service.";
		setarray @dtswarpmap$[0],"Toy Factory, Level 2","Clock Tower, Level 3";
		setarray @dtswarp$[0],@dtswarpmap$[0]+" -> 4,000z",
		@dtswarpmap$[1]+" -> 4,000z","Cancel";
		if($dts&(1<<5)){ 
			set @dtswarpmap$[2],"Lava Dungeon, Level 2";
			setarray @dtswarp$[2],@dtswarpmap$[2]+" -> 4,000z","Cancel";
		}
		break;
	}
	next;
	switch(select(@dtswarp$[0],@dtswarp$[1],@dtswarp$[2],@dtswarp$[3])){
		case 1:
			set @num, 0;
			break;
		case 2:
			set @num, 1;
			break;
		case 3:
			set @num, 2;
			break;
		case 4:
			set @num, 3;
			break;
	}
	Lwarp:
		if (@dtswarp$[@num] == "Cancel") goto Lcancel;
		if (Zeny<4000) goto Lnomoney;
		set Zeny, Zeny-4000;
		if (@dtswarpmap$[@num] == "Toy Factory, Level 2") warp "xmas_dun02",130,123;
 //Maps to warp to
		if (@dtswarpmap$[@num] == "Clock Tower, Level 3") warp "alde_dun03",265,22;

		if (@dtswarpmap$[@num] == "Lava Dungeon, Level 2") warp "mag_dun02",47,40;

		if (@dtswarpmap$[@num] == "Byalan, Level 4") warp "iz_dun03",32,63;

		if (@dtswarpmap$[@num] == "Clock Tower, Basement 3") warp "alde_dun03",277,178;

		if (@dtswarpmap$[@num] == "Glastheim Entrance") warp "glast_01",370,304;

		cutin "", 255;
		end;
	Lnomoney:
		switch(getarg(0)){
		case 0:
			mes "[Cool Event Corp. Voting Staff]"; 		//Unofficial Text
			mes "Umm, apparamment vous n'avez pas les fonds adéquats pour le warp choisi.";
                        mes "Je vous prie de vérifier que vous ayez le montant correc et d'essayer plus tard.";
			break;
		case 1:
			mes "[Kafra Voting Staff]";
			mes "Il semble que vous n'avez aps assez de Zenies pour le warp.";
			mes "Veuillez vérifier vos fonds et recommencez.";
			break;
		}
		goto Lend;						//End unofficial text

	Lcancel: 							//Selected Cancel on first menu
		switch(getarg(0)){
		case 0:
			mes "[Cool Event Corp. Voting Staff]";	
			mes "Cool Event Corp. travaille toujours à faire en sorte que ses clients soient non-seulement staisfaits, mais pour surpasser toutes vos attentes.";
			mes "Merci et bonne journée.";
			break;
		case 1:
			mes "[Kafra Voting Staff]";
			mes "Nous, à Kafra, nous efforçons toujours de vous offrir les meilleurs services.";
                        mes "Nous espérons combler tous vos besoins d'aventures et vos attentes d'excellence.";
			break;
		}
		goto Lend;
	Lend:
		close2;
		cutin "", 255;
		end;

}

//Elections administration NPC (hidden)
-	script	DTS_Admin	-1,{
	OnSun0100:					//Works only at 1am on sunday
		if($dtsday<4 || $dtsday>=100) set $dtsday,4;
		else set $dtsday,$dtsday+1;
		if($dts == 0){
			set $dts,$dts|(1<<0);
			set $dts,$dts|(1<<3);
		}
		if($dts&(1<<1) && $dts&(1<<2) || $dts&(1<<0) && $dts&(1<<1) || $dts&(1<<0) && $dts&(1<<2)){
			if($dts&(1<<0)) set $dts,$dts&~(1<<0);
			if($dts&(1<<1)) set $dts,$dts&~(1<<1);
			if($dts&(1<<2)) set $dts,$dts&~(1<<2);
			set $dts,$dts|(1<<0);
		}
		if($dts&(1<<4) && $dts&(1<<5) || $dts&(1<<3) && $dts&(1<<4) || $dts&(1<<3) && $dts&(1<<5)){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			set $dts,$dts|(1<<3);
		}
		if($dtsv == 0){				//If tied, gives random side 100 votes
			set $@dtstemp,rand(1,2);
			if($@dtstemp == 1) set $dtsv,$dtsv+100;
			else set $dtsv,$dtsv-100;
		}
		if($dtsv > 0){
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<1);
				set $dts,$dts|(1<<3);
			}
			if($dts&(1<<1)) set $dts,$dts|(1<<4);
			if($dts&(1<<2)){
				set $dts,$dts&~(1<<2);
				set $dts,$dts|(1<<1);
				set $dts,$dts|(1<<5);
			}
			set $dtsv,0;
		}
		else{
			if($dts&(1<<3)) set $dts,$dts&~(1<<3);
			if($dts&(1<<4)) set $dts,$dts&~(1<<4);
			if($dts&(1<<5)) set $dts,$dts&~(1<<5);
			if($dts&(1<<0)){
				set $dts,$dts&~(1<<0);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<3);
			}
			if($dts&(1<<1)){
				set $dts,$dts&~(1<<1);
				set $dts,$dts|(1<<2);
				set $dts,$dts|(1<<4);
			}
			if($dts&(1<<2)) set $dts,$dts|(1<<5);
			set $dtsv,0;
		}
		end;
}
